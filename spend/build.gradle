apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: "androidx.navigation.safeargs.kotlin"

static def stringField(String s) {
    return '"' + s + '"'
}

android {
    signingConfigs {
        config {
            storeFile file('/home/alex/StudioProjects/key.jks')
            storePassword key_store_password
            keyAlias 'spend'
            keyPassword spend_signing_password
        }
    }
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.qwert2603.spend"
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 58
        versionName "4.13.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "Long", "BIULD_TIME", System.currentTimeMillis() + "L"
        buildConfigField "String", "BIULD_HASH", '"' + UUID.randomUUID().toString() + '"'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-snapshot"
        }
        release {
            shrinkResources true
            minifyEnabled true
//            useProguard false
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-toolbar-icon'
            ]
        }
    }

    flavorDimensions "server", "aim"

    productFlavors {
        serverTest {
            dimension "server"
            applicationIdSuffix ".serverTest"
            versionNameSuffix "-serverTest"
            buildConfigField "String", "SERVER_TOKEN", stringField(token_spend_test)
        }
        serverProd {
            dimension "server"
            applicationIdSuffix ".serverProd"
            versionNameSuffix "-serverProd"
            buildConfigField "String", "SERVER_TOKEN", stringField(token_spend_prod)
        }
        serverMother {
            dimension "server"
            applicationIdSuffix ".serverMother"
            versionNameSuffix "-serverMother"
            buildConfigField "String", "SERVER_TOKEN", stringField(token_spend_mother)
        }
        serverAa {
            dimension "server"
            applicationIdSuffix ".serverAlexAnya"
            versionNameSuffix "-serverAlexAnya"
            buildConfigField "String", "SERVER_TOKEN", stringField(token_spend_aa)
        }

        forTesting {
            dimension "aim"
            applicationIdSuffix ".forTesting"
            versionNameSuffix "-forTesting"
        }
        forMarket {
            dimension "aim"
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

androidExtensions {
    experimental = true
}

dependencies {

    def appcompat_version = '1.1.0-beta01'
    def material_version = '1.0.0'
    def core_ktx_version = '1.2.0-alpha02'
    def room_version = '2.4.0'
    def work_version = "2.7.1"
    def nav_version = "2.3.5"

    def andrlib_version = '1.14.4'

    def rxjava_version = '2.2.9'
    def rxandroid_version = '2.1.1'
    def rxbinding_version = '3.0.0-alpha2'

    def mosby_version = '3.1.1'

    def koin_version = '2.0.1'
    def retrofit_version = '2.9.0'
    def okhttp_version = '4.9.3'

    def firebase_bom_version = '29.0.3'

    def vector_integer_view_version = '1.3.0'
    def fastscroll_version = '2.0.0'

    def stetho_version = '1.5.1'
    def junit_version = '4.12'
    def android_test_runner_version = '1.1.0-alpha4'

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.core:core-ktx:$core_ktx_version"

    implementation "com.github.qwert2603:andrlib:$andrlib_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.work:work-runtime-ktx:$work_version"

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
//    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "org.koin:koin-android:$koin_version"

    implementation("com.hannesdorfmann.mosby3:mvi:$mosby_version") {
        exclude group: 'com.hannesdorfmann.mosby3', module: 'utils-fragment'
    }

    implementation "io.reactivex.rxjava2:rxjava:$rxjava_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    implementation "com.jakewharton.rxbinding3:rxbinding:$rxbinding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-core:$rxbinding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rxbinding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:$rxbinding_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'

    implementation "com.github.qwert2603:VectorIntegerView:$vector_integer_view_version"
    implementation "com.simplecityapps:recyclerview-fastscroll:$fastscroll_version"

    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test:runner:$android_test_runner_version"

    forTestingImplementation "com.facebook.stetho:stetho:$stetho_version"
}

apply plugin: 'com.google.firebase.crashlytics'